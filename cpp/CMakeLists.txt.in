# Copyright (C) 2013 Columbia University in the City of New York and others.
#
# Please see the AUTHORS file in the main source directory for a full list
# of contributors.
#
# This file is part of TerraFERMA.
#
# TerraFERMA is free software: you can redistribute it and/or modify
# it under the terms of the GNU Lesser General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# TerraFERMA is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
# GNU Lesser General Public License for more details.
#
# You should have received a copy of the GNU Lesser General Public License
# along with TerraFERMA. If not, see <http://www.gnu.org/licenses/>.

# Require CMake 2.8
cmake_minimum_required(VERSION 2.8)

# set the project name (must be set before calling find_*(modules like vtk)
project(TERRAFERMA)

# Set verbose output while testing CMake
#set(CMAKE_VERBOSE_MAKEFILE 1)

set(REQUIRED_TERRAFERMA_VERSION "@TERRAFERMA_VERSION_SHORT@")

# Set CMake behavior
cmake_policy(SET CMP0004 OLD)

# Default build type (can be overridden by user)
set(CMAKE_BUILD_TYPE "RelWithDebInfo" CACHE STRING
  "Choose the type of build, options are: Debug MinSizeRel Release RelWithDebInfo.")

set(EXECUTABLE "terraferma" CACHE STRING
     "The name of the executable")

set(LOGLEVEL "INFO" CACHE STRING
     "The log level used in the run target, options are: DBG, TRACE, PROGRESS, INFO, WARNING, ERROR, CRITICAL.")

set(CMAKE_MODULE_PATH $ENV{CMAKE_MODULE_PATH})
find_package(BUCKETTOOLS ${REQUIRED_TERRAFERMA_VERSION} EXACT REQUIRED)

if (NOT BUCKETTOOLS_FOUND)
  message(FATAL_ERROR "Could not find BucketTools.")
endif()

set(OPTIONSFILE CACHE FILEPATH "Choose the input tfml file.")

if(NOT IS_ABSOLUTE ${OPTIONSFILE})
  set(OPTIONSFILE ${PROJECT_BINARY_DIR}/${OPTIONSFILE})
endif(NOT IS_ABSOLUTE ${OPTIONSFILE})

if (IS_DIRECTORY ${OPTIONSFILE})
  message(FATAL_ERROR "OPTIONSFILE not set or set to a directory.")
endif(IS_DIRECTORY ${OPTIONSFILE})

if (NOT EXISTS ${OPTIONSFILE})
  message(FATAL_ERROR "Could not find OPTIONSFILE ${OPTIONSFILE}")
endif(NOT EXISTS ${OPTIONSFILE})

# Get DOLFIN configuration data (DOLFINConfig.cmake must be in CMAKE_CONFIG_PATH)
find_package(DOLFIN ${BUCKETTOOLS_DOLFIN_VERSION_STR} EXACT REQUIRED)

# include the buckettools include directory and the binary directory (for the systemswrapper)
include_directories(${PROJECT_BINARY_DIR})

include_directories(${BUCKETTOOLS_INCLUDE_DIRS})
include_directories(SYSTEM ${BUCKETTOOLS_DEP_INCLUDE_DIRS})

add_definitions(${BUCKETTOOLS_CXX_DEFINITIONS})
set(CMAKE_CXX_FLAGS "${BUCKETTOOLS_CXX_FLAGS} ${CMAKE_CXX_FLAGS}")

# Add Mac specific loader flags
if (APPLE)
  set(CMAKE_SHARED_LINKER_FLAGS "-Wl,-undefined,dynamic_lookup" CACHE STRING "should only be set if macosx" FORCE)
endif()

# Need to get VTK config because VTK uses advanced VTK features which
# mean it's not enough to just link to the DOLFIN target. See
# http://www.vtk.org/pipermail/vtk-developers/2013-October/014402.html
find_package(VTK HINTS ${VTK_DIR} $ENV{VTK_DIR} NO_MODULE QUIET)

add_subdirectory(${BUCKETTOOLS_SHARED_DIRS}/buckettools/ufc "buckettools_ufc")

add_executable(${EXECUTABLE} ${PROJECT_SOURCE_DIR}/main.cpp)

target_link_libraries(${EXECUTABLE} ${BUCKETTOOLS_LIBRARIES} buckettools_ufc)

add_custom_target(
  run
  COMMAND ${EXECUTABLE} -v${LOGLEVEL} -l ${OPTIONSFILE} 
  WORKING_DIRECTORY ${PROJECT_BINARY_DIR}
  DEPENDS ${EXECUTABLE}
  )

